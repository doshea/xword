window.team_app =
  solver_red: (Math.random()*266).floor()
  solver_green: (Math.random()*266).floor()
  solver_blue: (Math.random()*266).floor()
  cell_clack_sound: new Audio('<%= audio_path('chuck.mp3') %>')
  last_clack: null
  message_received_sound: new Audio('<%= audio_path('submarine.m4a') %>')
  last_received: null
  unread_count: 0
  display_name: team_app.display_name
  solver_id: team_app.solver_id
  outline_timer: null

  #------------------
  # GENERAL FUNCTIONS
  #------------------

  toggle_chat: ->
    $('#team-chat').toggleClass('down')
    unless $('#team-chat').hasClass 'down'
      team_app.unread_count = 0
      $('#unread-chat-count').text ''
    $('#chat-minmax').toggleClass('fi-minus fi-plus')

  #------------------
  # PUSHER SENDS
  #------------------

  # Triggers a 'team_update' Pusher message out on this team solution's channel, instructing all
  # other participants to update their visible crossword with the updated cell
  send_team_cell: ($cell, letter) ->
    token = $('#crossword').data('auth-token')
    settings =
      type: 'PATCH'
      url: "/solutions/#{solve_app.solution_id}/team_update"
      data: {authenticity_token: token, letter: letter, col: $cell.data('col'), row: $cell.data('row'), channel: channel, solver_id: team_app.solver_id, red: team_app.solver_red, green: team_app.solver_green, blue: team_app.solver_blue}
    $.ajax(settings)

  # Triggers a 'join_team' Pusher message out on this team solution's channel, instructing all
  # other participants to update their visible rosters by adding this participant's color and name
  join_team: ->
    token = $('#crossword').data('auth-token')
    settings =
      type: 'POST'
      url: "/solutions/#{solve_app.solution_id}/join_team"
      data: {authenticity_token: token, channel: channel, display_name: team_app.display_name, red: team_app.solver_red, green: team_app.solver_green, blue: team_app.solver_blue, solver_id: team_app.solver_id}
    $.ajax(settings)

  # Triggers a 'leave_team' Pusher message out on this team solution's channel, instructing all
  # other participants to update their visible rosters by removing this participant's color and name
  leave_team: ->
    token = $('#crossword').data('auth-token')
    settings =
      type: 'POST'
      url: "/solutions/#{solve_app.solution_id}/leave_team"
      data: {authenticity_token: token, channel: channel, solver_id: team_app.solver_id}
    $.ajax(settings)

  # Triggers a 'roll_call' Pusher message out on this team solution's channel, instructing all
  # participants to join the team
  # TODO: Figure out why this isn't properly getting all teammates
  roll_call: ->
    token = $('#crossword').data('auth-token')
    settings =
      type: 'POST'
      url: "/solutions/#{solve_app.solution_id}/roll_call"
      data: {authenticity_token: token, channel: channel}
    $.ajax(settings)

  # Triggers a 'show_team_clue' Pusher message out on this team solution's channel, instructing all
  # other participants to temporarily outline that clue
  # TODO: Figure out why this isn't properly getting all teammates
  show_team_clue: ->
    token = $('#crossword').data('auth-token')
    cell_num = $(this).data('cell-num')
    across = $(this).hasClass('across-clue')
    settings =
      type: 'POST'
      url: "/solutions/#{solve_app.solution_id}/show_team_clue/"
      data: {authenticity_token: token, channel: channel, cell_num: cell_num, across: across, red: team_app.solver_red, green: team_app.solver_green, blue: team_app.solver_blue, solver_id: team_app.solver_id}
    $.ajax(settings)

  #------------------
  # PUSHER RECEIVES
  #------------------

  # When a 'team_update' Pusher message is received on this channel (updating a cell's contents),
  # if this window was not the original sender, updates the cell and flashes the sender's RGB color on that cell
  receive_team_cell: (data)->
    unless team_app.solver_id is data.solver_id
      $cell = $(".cell[data-row=#{data.row}][data-col=#{data.col}]")
      now = Date.now()
      unless (team_app.last_clack and (now-team_app.last_clack < 4000))
        team_app.last_clack = now
        team_app.cell_clack_sound.play()
      $cell.children('.flag').stop().css('background', "rgb(#{data.red}, #{data.green}, #{data.blue})").animate({opacity: 1}, 300).delay(300).animate({opacity: 0}, 200);
      if data.letter is ''
        $cell.delete_letter(false)
        # solve_app.update_unsaved()
      else
        $cell.set_letter(data.letter, false)
        # solve_app.update_unsaved()

  # Triggered when a 'join_puzzle' Pusher message is received. Adds a new square to the roster of the joining teammate's color
  welcome_teammate: (data) ->
    unless $(".teammate-box[solver_id=#{data.solver_id}]").length > 0
      teammate = $('<div>')
      teammate.addClass('teammate-box has-tip').css('background',  "rgb(#{data.red}, #{data.green}, #{data.blue})").attr('data-tooltip', true).attr('title', data.display_name).attr('solver_id', data.solver_id)
      $('#teammates').append(teammate)

  # Triggered when a 'join_puzzle' Pusher message is received. Removes the roster square of the leaving teammate
  farewell_teammate: (data) ->
    console.log('So long, farewell')
    $("#teammates .teammate-box[solver_id=#{data.solver_id}]").fadeOut 1000, ->
      $(this).remove()

  # Triggered when a 'chat_message' Pusher message is received. If the message originated from this sender, adds a
  # sender-styled chat to the chat history. If it is from another sender, adds an other-styled chat to the chat history.
  receive_chat: (data) ->
    is_my_chat = (team_app.display_name is data['display_name'])
    chats_already_exist = ($('.chat').length > 0)

    chats = $('#team-chat')
    new_chat = $('<div>')
    new_chat.addClass('chat')
    new_chat.addClass('my-chat') if is_my_chat

    avatar = $('<img>')
    display_name = $('<span>') unless is_my_chat
    chat_text = $('<span>')

    avatar.attr('src', data['avatar']).addClass('avatar')
    display_name.text(data['display_name']+': ').addClass('display-name') unless is_my_chat
    chat_text.text(data['chat_text']).addClass('chat-text')

    avatar.appendTo(new_chat)
    unless is_my_chat
      display_name.appendTo(new_chat)
    if is_my_chat then chat_text.prependTo(new_chat) else chat_text.appendTo(new_chat)
    if chats_already_exist then $('<hr />').appendTo($('#chats'))
    new_chat.appendTo($('#chats'))
    unless is_my_chat
      new_chat.css('background-color': '#ff7878')
      if chats.hasClass('down')
        team_app.unread_count += 1
        $('#unread-chat-count').text("(#{team_app.unread_count})")
      now = Date.now()
      unless (team_app.last_received and (now-team_app.last_received < 8000))
        team_app.last_received = now
        team_app.message_received_sound.play()
      window.setTimeout (->
        new_chat.css('background-color': 'white')
      ), 0.4
    chats_scroll_length = $('#chats').get(0).scrollHeight
    $('#chats').stop().animate({scrollTop: chats_scroll_length}, 'fast')


  # Triggered when a 'outline_team_clue' Pusher message is received from another user. Makes and/or moves the hidden outline
  # to cover the word in question, then fades it in and changes its color to match the user who outlined it
  outline_team_clue: (data) ->
    unless data.solver_id is team_app.solver_id
      outline_width = 2
      outline_id = "#{data.solver_id}_word_highlight"
      outline_color = "#{data.red}, #{data.green}, #{data.blue}"
      the_outline = $("##{outline_id}")
      unless the_outline.get(0)
        the_outline = $("<div id='#{outline_id}'>")
        the_outline.addClass('team-outline').hide().css('border', "#{outline_width}px dashed rgb(#{outline_color})").css('background-color', "rgba(#{outline_color}, 0.2)")
        $('body').append(the_outline)

      across = data['across'] is 'true'
      start_cell = $(".cell[data-cell=#{data.cell_num}]")
      word_cells = if across then start_cell.get_across_word_cells() else start_cell.get_down_word_cells()
      number_of_cells = word_cells.length
      height_of_outline = if across then word_cells[0].height()-3 else word_cells[0].height()*number_of_cells+2*number_of_cells-5
      width_of_outline = if across then word_cells[0].width()*number_of_cells+2*number_of_cells-5 else word_cells[0].width()-3
      left_corner_x = word_cells[0].offset().left+1
      left_corner_y = word_cells[0].offset().top+1
      the_outline.height(height_of_outline)
      the_outline.width(width_of_outline)
      the_outline.offset({ top: left_corner_y, left: left_corner_x })
      the_outline.stop().css('opacity', 1).show()

      if team_app.outline_timer then clearInterval(team_app.outline_timer)
      team_app.outline_timer = setTimeout (->
        the_outline.animate
          'opacity': 0
        , 'fast'
      # Do something after 5 seconds
      ), 20000

#------------------
# DOCUMENT LOAD
#------------------
$ ->
  $('#team-explanation').foundation('reveal', 'open');
  listening_channel = pusher.subscribe(channel)
  listening_channel.bind('change_cell', team_app.receive_team_cell)
  listening_channel.bind('join_puzzle', team_app.welcome_teammate)
  listening_channel.bind('leave_puzzle', team_app.farewell_teammate)
  listening_channel.bind('roll_call', team_app.join_team)
  listening_channel.bind('chat_message', team_app.receive_chat)
  listening_channel.bind('outline_team_clue', team_app.outline_team_clue)
  team_app.roll_call()
  $('#team-chat .row-topper').on('click', team_app.toggle_chat)
  $('.clue').on('click', team_app.show_team_clue)

  $(window).on('unload', team_app.leave_team)


